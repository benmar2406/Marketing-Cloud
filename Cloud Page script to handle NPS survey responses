

%%[

    VAR @accountid, @nps, @npsInt, @createSurveyResponse, @surveyName, @vertragsid, @existingSurveys, @rowCount, @ResponseConditions
    /* Übernehme Paramater Values aus dem Mailing */
    SET @nps = QueryParameter("nps")
    SET @accountid = QueryParameter("Id")
    SET @surveyName = QueryParameter("sn")
    SET @vertragsid = QueryParameter("vid")
    /*wandle den nps String in einen Int Wert um */
    SET @npsInt = FormatNumber(@nps,"G")
    SET @ResponseConditions = false

    /*Prüfe anhand der aktuellen Vertrags Id ob es bestehende Records zum aktuellen Vertrag gibt */
    SET @existingSurveys = RetrieveSalesforceObjects(
      'Surveymonkey_Response__c', 
      'Name', 
      'Vertrag__c', '=', @vertragsid)

    SET @rowCount = rowcount(@existingSurveys) 
    IF (@rowCount == 0) THEN
        /* wenn noch keine Survey zum Vertrag exisitert kann diese erstellt werden */
        SET @ResponseConditions = true

    ELSEIF (@rowCount > 0) THEN
        VAR @row, @counter
        /* wenn mindestens eine Survey existiert loope durch die Records und... */
        FOR @counter = 1 to @rowCount DO
            SET @row = row(@existingSurveys, @counter)
            SET @exSurveyName = field(@row,"Name")
            /*...prüfe, ob es sich um die gleiche oder eine andere Survey handelt bzw. bereits eine Bewertung vorgenommen wurde (aktuell sind die Umfragen nach 4 Monaten und nach 6 Monaten angesetzt) */
            IF (@exSurveyName == @surveyName)
                THEN /* Mache nichts, da bereits eine Bewertung durch den Kunden mit dem gleichen Vertrag zur gleichen Umfrage abgegeben wurde*/
            ELSE
                /*wenn keiner der Surveynames übereinstimmt wurde noch keine Umfrage abgegeben*/
                SET @ResponseConditions = true
            ENDIF
            NEXT

    ENDIF
    /* Wenn die obigen Kriterien erfüllt wurden, erstelle die Response in der Sales Cloud anhand der aus dem Mailing übergebenen Werte*/
    IF @ResponseConditions == true THEN
    
      SET @createSurveyResponse = CreateSalesforceObject(
        "Surveymonkey_Response__c", 7,
        "Name", @surveyName,
        "Account__c", @accountid,
        "Promoter_Score__c", @npsInt,
        "Response_ID__c", "MC Response",
        "Response_Status__c", "completed",
        "Vertrag__c", @vertragsid,
        "BU__c", "SGD"
       )
        /* Stelle den Danketext dar */    
        ]%%
        <div style="text-align: center;"><b><span style="font-family:Arial,Helvetica,sans-serif; font-size:22px; color:#89C82F;">Vielen Dank f&uuml;r Ihre R&uuml;ckmeldung. Wir haben Ihre Bewertung mit aufgenommen.</span></b></div>
    
    %%[ ELSE ]%%
        <div style="text-align: center;"><b><span style="font-family:Arial,Helvetica,sans-serif; font-size:22px; color:#89C82F;">Entschuldigung, Sie haben die Bewertung bereits abgeschlossen.</span></b></div>
    %%[ ENDIF 
]%%
