/* Cloud page script that handles the submission of a NPS rating via a link from an email. Checks if a submission was already made for the contract */

%%[

    VAR @accountid, @nps, @npsInt, @createSurveyResponse, @surveyName, @vertragsid, @existingSurveys, @rowCount, @ResponseConditions
    /* Take Parameter Values from the Mailing */
    SET @nps = QueryParameter("nps")
    SET @accountid = QueryParameter("Id")
    SET @surveyName = QueryParameter("sn")
    SET @vertragsid = QueryParameter("vid")
    /* Convert the nps String into an Int value */
    SET @npsInt = FormatNumber(@nps,"G")
    SET @ResponseConditions = false

    /* Check based on the current contract Id if there are existing records for the current contract */
    SET @existingSurveys = RetrieveSalesforceObjects(
      'Surveymonkey_Response__c', 
      'Name', 
      'Vertrag__c', '=', @vertragsid)

    SET @rowCount = rowcount(@existingSurveys) 
    IF (@rowCount == 0) THEN
        /* if no survey exists for the contract, it can be created */
        SET @ResponseConditions = true

    ELSEIF (@rowCount > 0) THEN
        VAR @row, @counter
        /* if at least one survey exists, loop through the records and... */
        FOR @counter = 1 to @rowCount DO
            SET @row = row(@existingSurveys, @counter)
            SET @exSurveyName = field(@row,"Name")
            /*...check if it is the same or a different survey or if a rating has already been made (currently, the surveys are set after 4 months and after 6 months) */
            IF (@exSurveyName == @surveyName)
                THEN /* Do nothing, as a rating has already been submitted by the customer with the same contract for the same survey */
            ELSE
                /*if none of the survey names match, no survey has been submitted yet*/
                SET @ResponseConditions = true
            ENDIF
            NEXT

    ENDIF
    /* If the above criteria have been met, create the response in the Sales Cloud based on the values passed from the mailing*/
    IF @ResponseConditions == true THEN
    
      SET @createSurveyResponse = CreateSalesforceObject(
        "Surveymonkey_Response__c", 7,
        "Name", @surveyName,
        "Account__c", @accountid,
        "Promoter_Score__c", @npsInt,
        "Response_ID__c", "MC Response",
        "Response_Status__c", "completed",
        "Vertrag__c", @vertragsid,
        "BU__c", "ACME placeholder"
       )
        /* Display the thank you text */    
        ]%%
        <div style="text-align: center;"><b><span style="font-family:Arial,Helvetica,sans-serif; font-size:22px; color:#89C82F;">Thank you for your feedback. We have recorded your evaluation.</span></b></div>
    
    %%[ ELSE ]%%
        /* Display message that submission already was made */   
        <div style="text-align: center;"><b><span style="font-family:Arial,Helvetica,sans-serif; font-size:22px; color:#89C82F;">Sorry, you have already completed the evaluation.</span></b></div>
    %%[ ENDIF 
]%%
