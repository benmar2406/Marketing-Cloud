/*This script generates an ical event based on paramters from a campaign */

<script runat="server">
try{
</script>
%%[
    VAR @dtstamp, @uniqueId, @cmid, @campaignDescription, @campaignName, @eventLink, @eventStartDateTime, @eventEndDateTime, @cId, @encodedEventLink
    SET @ORGANIZER_txt = 'Klett Corporate Education'
    SET @dtstamp = Format(Now(), "yyyyMMddTHHmmss")
    SET @UID_txt = Concat("KCE_", @dtstamp)
    SET @cmid = RequestParameter("cmid")
    
    /*Check for available contact data, otherwise stick to the journey data */
    SET @campaignMembers = LookupRows("CampaignMember_Campaigns", "CampaignMemberId", @cmid)
    
    IF RowCount(@campaignMembers) > 0 THEN
        SET @cmember = row(@campaignMembers, 1)
        
        SET @campaignDescription = field(@cmember, "Beschreibung_Kampagne")
        SET @campaignName = field(@cmember, "Name_Kampagne")
        SET @eventStartDateTime = field(@cmember, "Event_Start")
        SET @eventEndDateTime = field(@cmember, "Event_Ende")
        SET @isDst = field(@cmember, "Event_in_DST")
        SET @@cId = field(@cmember, "ContactId")
        SET @eventStartDateTimeParam = Format(@eventStartDateTime, "yyyy-MM-ddTHH:mm:ss", "", "UTC")
        SET @eventEndDateTimeParam = Format(@eventEndDateTime, "yyyy-MM-ddTHH:mm:ss", "", "UTC") 
        SET @eventLink = field(@cmember, "Event_Link")
        SET @encodedEventLink = URLEncode(@eventLink, 1, 1)
        
    ELSE
        SET @campaignDescription = QueryParameter("campaignDescription")
        SET @campaignName = QueryParameter("campaignName")
        SET @eventStartDateTime = DateParse(RequestParameter("eventStartDateTime"))
        SET @eventEndDateTime = DateParse(RequestParameter("eventEndDateTime"))
        SET @isDst = QueryParameter("isDst")
        SET @cId = QueryParameter("cid")
        SET @eventStartDateTimeParam = Format(@eventStartDateTime, "yyyy-MM-ddTHH:mm:ss", "", "UTC")
        SET @eventEndDateTimeParam = Format(@eventEndDateTime, "yyyy-MM-ddTHH:mm:ss", "", "UTC")
        SET @encodedEventLink = QueryParameter("eventLink")
    ENDIF



    /*Set to Event handler Cloud Page: Not directly to actual event page. CP 1157 is always required for event handling*/
 
    SET @eventHandlerUrl =  CloudPagesURL(1157, "cid", @cId, "cmid", @cmid, "eventStartDateTime", @eventStartDateTimeParam, "eventEndDateTime", @eventEndDateTimeParam, "isDst", @isDst, "eventLink", @encodedEventLink)
    SET @DTSTAMP_txt = Concat(Format(Now(),"yyyyMMdd","Date"), "T", Format(Now(),"HHmmss","Date"))
    SET @DTSTART_txt = Concat(Format(@eventStartDateTime,"yyyyMMdd","Date"), "T", Format(@eventStartDateTime,"HHmmss","Date"))
    SET @DTEND_txt = Concat(Format(@eventEndDateTime,"yyyyMMdd","Date"), "T", Format(@eventEndDateTime,"HHmmss","Date"))
    SET @LOCATION_txt = CONCAT('Microsoft Teams')
    SET @ICS_DESCRIPTION_text = CONCAT(@campaignDescription, " ", @eventHandlerUrl)
    SET @ICS_SUMMARY_text = @campaignName
    SET @PRODID_txt = "-//KCE_MC//DE"
    SET @htmlDescription = "<!DOCTYPE html><html><body><h1>%%=v(@campaignName)=%%</h1><p>Hier geht's zur Veranstaltung:</p><br><a href='%%=v(@eventHandlerUrl)=%%' style='background-color:#EB5D45; color:white; padding:10px 20px; text-align:center; display:inline-block; text-decoration:none; border-radius: 5px;'>Zum Event</a><br><br></body></html>"
    /*Create ical file*/
    SET @icsMSG = Concat(
    "BEGIN:VCALENDAR\n", 
    "CALSCALE:GREGORIAN\n",
    "METHOD:PUBLISH\n",
    "VERSION:2.0\n",
    "PRODID:", @PRODID_txt, "\n",
    "BEGIN:VFREEBUSY",
    "FREEBUSY;FBTYPE=FREE:", @FREEBUSY_txt, "\n",  
    "ORGANIZER;CN=", @ORGANIZER_txt, "\n",
    "UID:", @UID_txt, "\n",
    "DTSTAMP:", @DTSTAMP_txt, "\n",
    "END:VFREEBUSY\n",
    "BEGIN:VEVENT\n",
    "CLASS:PUBLIC\n",
    "DESCRIPTION:", TreatAsContent(@ICS_DESCRIPTION_text), "\n",
    "X-ALT-DESC;FMTTYPE=text/html:", TreatAsContent(@htmlDescription), "\n", 
    "DTSTART:", @DTSTART_txt, "\n",
    "DTSTAMP:", @DTSTAMP_txt, "\n",
    "DTEND:", @DTEND_txt, "\n",
    "LOCATION:", @LOCATION_txt, "\n",
    "PRIORITY:5\n",
    "TRANSP:TRANSPARENT\n",
    "SEQUENCE:0\n",
    "STATUS:CONFIRMED\n",
    "SUMMARY:", @ICS_SUMMARY_text, "\n",
    "UID:", @UID_txt, "\n",
    "BEGIN:VALARM\n",
    "TRIGGER:-PT30M\n",
    "ACTION:DISPLAY\n",
    "DESCRIPTION:", TreatAsContent(@ICS_DESCRIPTION_text), "\n",
    "END:VALARM\n",
    "END:VEVENT\n",
    "END:VCALENDAR"
    )
]%%

<script runat="server">
}catch(e){
Write(Stringify(e));
}
</script>
<!doctype html>
<html>
<head>
<meta charset="utf-8">
<title>Download Kalendereintrag</title>
<meta name="description" content="">
<meta name="ROBOTS" content="NOINDEX, NOFOLLOW">
<meta name="viewport" content="width=device-width, initial-scale=1">  
</head>
<body>

<script type="text/javascript" runat="client"> 
var icsMSG = "%%=v(@icsMSG)=%%";
download("Ihr_Kalendereintrag.ics",icsMSG);

function download(filename, text) {
var element = document.createElement('a');
element.setAttribute('href', 'data:text/plain;charset=utf-8,' + encodeURIComponent(text));
element.setAttribute('download', filename);

element.style.display = 'none';
document.body.appendChild(element);

element.click();

document.body.removeChild(element);
}
</script> 
</body>
</html>


