<!==

%%[ 
    VAR @cmid, @campaignDescription, @campaignName, @eventLink, @eventStartDateTime, @eventEndDateTime, @linkToIcalGeneration, @isDst,  @cId
    SET @cmid = AttributeValue("CampaignMember:Id")
    SET @campaignDescription = AttributeValue("CampaignMember:Campaign:Description")
    SET @campaignName = AttributeValue("CampaignMember:Campaign:Name")
    SET @eventLink = AttributeValue("CampaignMember:Campaign:Event_Link__c")
    SET @encodedEventLink = URLEncode(@eventLink,1,1)
    
    SET @cId = AttributeValue("CampaignMember:Contact:Id")

    SET @campaignMembers = LookupRows("CampaignMember_Campaigns", "CampaignMemberId", @cmid)

   /*Check if contact data is available*/
   IF RowCount(@campaignMembers) > 0 THEN
        SET @cmember = row(@campaignMembers, 1) 
        SET @eventStartDateTime = field(@cmember, "Event_Start")
        SET @eventEndDateTime = field(@cmember, "Event_Ende")
        SET @isDst = field(@cmember, "Event_in_DST")
        SET @debug = "Contactdata"
        /*Adjust for daylight savings*/
        IF (@isDst == false) THEN
            SET @eventStartCET = DateAdd(@eventStartDateTime, 7, "H")
            SET @eventEndCET = DateAdd(@eventEndDateTime, 7, "H")
        ELSE 
            SET @eventStartCET = DateAdd(@eventStartDateTime, 8, "H")
            SET @eventEndCET = DateAdd(@eventEndDateTime, 8, "H")
        ENDIF
    
    ELSE
        /* If no contact data is available retrieve data from journey data extension. The offset needs to be calculated differently since times are displayed differently in SF Data Extensions und regular data extensions*/
        SET @isDst = AttributeValue("CampaignMember:Campaign:IsEventInDST__c")
        SET @debug = "Journeydata"
        SET @eventStartDateTime = AttributeValue("CampaignMember:Campaign:Event_Start_Datetime__c")
        SET @eventEndDateTime = AttributeValue("CampaignMember:Campaign:Event_End_Datetime__c")

        /*Adjust for daylight savings*/
         /*Since time is displayed differently in Salesforce Data Extensions and regular data extensions the offset needs to be calculated differntly in both scenarios.*/
        IF (@isDst == false) THEN
            SET @eventStartCET = DateAdd(@eventStartDateTime, 1, "H")
            SET @eventEndCET = DateAdd(@eventEndDateTime, 1, "H")
        ELSE 
            SET @eventStartCET = DateAdd(@eventStartDateTime, 2, "H")
            SET @eventEndCET = DateAdd(@eventEndDateTime, 2, "H")
        ENDIF
    ENDIF

    
    /*Format for displaying date and time in german format (in email)*/
    SET @formattedEventStartDate = Format(@eventStartCET, "dddd, d. MMMM yyyy, HH:mmtt", "", "de-DE")
    SET @displayEventDateTime = CONCAT(@formattedEventStartDate, " Uhr")
    SET @displayEventDate = Format(@eventStartCET, "dd.MM.yyyy", "", "de-DE")
    
    
     /*Format dateteime so that it can be transmitted as a parameter. On the cloudpage it will only be used if no contact data is available.*/
    SET @eventStartDateTimeParam = Format(@eventStartCET, "yyyy-MM-ddTHH:mm:ss", "", "UTC")
    SET @eventEndDateTimeParam = Format(@eventEndCET, "yyyy-MM-ddTHH:mm:ss", "", "UTC") 
    /*Cloud Page Links: Generate Link for event handling page and for the ical download page */
    SET @linkToIcalGeneration = CloudPagesURL(1162, "cid", @cId, "cmid", @cmid, "campaignDescription", @campaignDescription, "campaignName", @campaignName, "eventLink", @encodedEventLink, "eventStartDateTime", @eventStartDateTimeParam, "eventEndDateTime", @eventEndDateTimeParam, "isDst", @isDst)
    SET @linkToEventHandler = CloudPagesURL(1157, "cid", @cId, "cmid", @cmid, "eventStartDateTime", @eventStartDateTimeParam, "eventEndDateTime", @eventEndDateTimeParam, "isDst", @isDst, "eventLink", @encodedEventLink)
]%%

==>



